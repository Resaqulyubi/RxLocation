apply plugin: 'com.android.application'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.1"

    defaultConfig {
        applicationId "net.kjulio.rxlocationtest"
        minSdkVersion 10
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            testCoverageEnabled = true
        }
    }

    testBuildType "debug"

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }
}

dependencies {

    String butterknifeVersion = '8.2.1'
    String junitVersion = '4.12'
    String hamcrestVersion = '1.3'
    String testSupportVersion = '0.5'
    String espressoVersion = '2.2.2'
    String mockitoVersion = '1.10.19'

    compile project(':rxlocation')
    compile 'com.android.support:appcompat-v7:24.1.1'
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava:1.1.8'
    compile "com.jakewharton:butterknife:$butterknifeVersion"
    provided "com.jakewharton:butterknife-compiler:$butterknifeVersion"

    testCompile "junit:junit:$junitVersion"
    testCompile "org.hamcrest:hamcrest-integration:$hamcrestVersion"
    testCompile "org.mockito:mockito-core:$mockitoVersion"
    testCompile 'org.robolectric:robolectric:3.1.2'

    androidTestCompile "junit:junit:$junitVersion"
    androidTestCompile "org.hamcrest:hamcrest-integration:$hamcrestVersion"
    androidTestCompile("com.android.support.test:rules:$testSupportVersion", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestCompile("com.android.support.test:runner:$testSupportVersion", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestCompile("com.android.support.test.espresso:espresso-core:$espressoVersion", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

}
