apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'jacoco-android'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.3"

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 24
        versionName "$version"
        testApplicationId "net.kjulio.rxlocation.test"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled true
            // setting shrinkResources false causes the following error while running androidTests:
            //
            // A problem was found with the configuration of task ':rxlocation:packageDebugAndroidTest'.
            // > File './RxLocation/rxlocation/build/intermediates/res/resources-debug-androidTest-stripped.ap_'
            // specified for property 'resourceFile' does not exist.
            //
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testCoverageEnabled = true
        }
    }

    // Instrumented tests have to be run on the debug variant as the release variant requires
    // signing the test apk which is useless for library projects.
    testBuildType "debug"

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }

    lintOptions {
        abortOnError true
        xmlReport true
        htmlReport true
    }
}

dependencies {

    compile 'com.android.support:appcompat-v7:24.2.1'
    compile 'com.google.android.gms:play-services-location:9.6.1'
    compile 'io.reactivex:rxjava:1.2.1'

    testCompile 'io.reactivex:rxandroid:1.2.1'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.2.7'
    testCompile "org.robolectric:robolectric:3.1.2"

    androidTestCompile 'io.reactivex:rxandroid:1.2.1'
    androidTestCompile 'junit:junit:4.12'
    androidTestCompile 'com.android.support.test:runner:0.5'

}

group = 'net.kjulio.RxLocation'
version = '0.10.0-beta'

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    pkg {
        repo = 'RxLocation'
        name = 'RxLocation'

        version {
            name = project.version
            desc = 'An Android Library to get location updates with a nice and clean rxJava API.'
            released  = new Date()
            vcsTag = 'v' + project.version
        }

        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/julioromano/RxLocation.git'
        websiteUrl = 'https://github.com/julioromano/RxLocation'
        publish = true
    }
    configurations = ['archives']
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}
